buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.23'
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.3'
    id 'io.freefair.lombok' version '5.3.3.3'
    id 'org.flywaydb.flyway' version '7.12.1'
    id 'org.sonarqube' version '3.3'
    id 'java'
    id 'jacoco'
}

ext {
    guavaVersion = '30.1.1-jre'
    r2dbcPostgresVersion = '0.8.8.RELEASE'
    r2dbcSpiVersion = '0.8.5.RELEASE'
    r2dbcPoolVersion = '0.8.5.RELEASE'
    r2dbcH2Version = '0.8.4.RELEASE'
    nettyVersion = '4.1.66.Final'
    jjwtVersion = '0.11.2'
    jaxbApiVersion = '2.3.1'
    flywayVersion = '7.12.1'
    bouncycastleVersion = '1.69'
    jodaTimeVersion = '2.10.10'
}

group = 'org.briarheart'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation("org.springframework.boot:spring-boot-starter-data-r2dbc") {
        exclude group: 'io.r2dbc'
    }
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework:spring-jdbc'
    implementation "io.r2dbc:r2dbc-postgresql:$r2dbcPostgresVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "io.jsonwebtoken:jjwt-api:$jjwtVersion"
    implementation "joda-time:joda-time:$jodaTimeVersion"

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly "io.r2dbc:r2dbc-spi:$r2dbcSpiVersion"
    runtimeOnly "io.r2dbc:r2dbc-pool:$r2dbcPoolVersion"
    runtimeOnly "org.flywaydb:flyway-core:$flywayVersion"
    runtimeOnly "io.netty:netty-codec-http:$nettyVersion"
    runtimeOnly "io.netty:netty-codec-http2:$nettyVersion"
    runtimeOnly "io.netty:netty-handler-proxy:$nettyVersion"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:$jjwtVersion"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"
    runtimeOnly "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    runtimeOnly "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "io.r2dbc:r2dbc-h2:$r2dbcH2Version"

    testRuntimeOnly 'com.h2database:h2'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    environment 'JDBC_DATABASE_URL', 'jdbc:h2:mem:testdb;DB_CLOSE_DELAY=-1;DB_CLOSE_ON_EXIT=FALSE;MODE=PostgreSQL'
    environment 'JDBC_DATABASE_USERNAME', 'sa'
    environment 'JDBC_DATABASE_PASSWORD', ''

    environment 'R2DBC_DATABASE_URL', 'r2dbc:h2:mem:testdb;DB_CLOSE_DELAY=-1;DB_CLOSE_ON_EXIT=FALSE;MODE=PostgreSQL'
    environment 'R2DBC_DATABASE_USERNAME', 'sa'
    environment 'R2DBC_DATABASE_PASSWORD', ''

    useJUnitPlatform()
}

jar {
    enabled = false
}
